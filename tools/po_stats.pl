#!/usr/bin/perl
use strict;
use warnings;

use Cwd                   qw{ cwd };
use File::Spec::Functions qw{ catfile catdir };
use File::Find::Rule;
use File::Basename        qw{ basename };
use File::Temp            qw{ tempdir };
use Env                   qw{ LANG };
use Getopt::Long          qw{ GetOptions };
#use Locale::PO;

my %reports;

my $text;
my $html;
my $dir;
my $project_dir;
my $details;
my $all;
my $trunk;
GetOptions(
	'text'      => \$text, 
	'html'      => \$html, 
	'dir=s'     => \$dir, 
	'project=s' => \$project_dir,
	'details'   => \$details,
	'all'       => \$all,
	'trunk=s'   => \$trunk,
	) or usage();
usage() if not $text and not $html;

$LANG = 'C';

my $tempdir = tempdir( CLEANUP => 1 );
my $cwd       = cwd;

usage("--all and --project are mutually exclusive") 
	if $all and $project_dir;
if (not $all and not $project_dir) {
	$project_dir = $cwd;
}


if ($project_dir) {
	$reports{basename $project_dir} = collect_report($project_dir);
} elsif ($all) {
	$trunk ||= $cwd;
	foreach my $project_dir ("$trunk/Padre", glob( "$trunk/Padre-Plugin-*" )) {
		#print "P: $project_dir\n";
		$reports{basename $project_dir} = collect_report($project_dir);
	}
}
	
my $text_report_file = catfile($cwd, 'po_report.txt');

if ($text) {
	save_text_report($text_report_file);
} elsif ($html) {
	usage("--dir was missing") if not $dir;
	usage("--dir $dir does not exist") if -d $dir;
}




sub collect_report {
	my ($project_dir) = @_;

	my %data;
        
	my $share = catdir ( $project_dir, 'share' );
	if (not -e $share) {
		($share) = File::Find::Rule->directory()->name('share')->in(catdir( $project_dir, 'lib'));
	}
	if (not $share or not -e $share) {
		warn("Could not find a 'share' directory in '$project_dir'");
		return;
	}

	my $localedir = catdir ( $share, 'locale' );

	if (not -d $localedir) {
		warn("Can't find locale directory '$localedir'.");
		return;
	}


	my @po_files  = glob "$localedir/*.po";
	my $pot_file  = catfile( $localedir, 'messages.pot' );
	foreach my $po_file (sort @po_files) {
		#print "$po_file\n";
		my $err = "$tempdir/err";
		system "msgcmp $po_file $pot_file 2> $err";
		my $language = basename($po_file);
		if (open my $fh, '<', $err) {
			local $/ = undef;
			$data{$language}{details} = <$fh>;
			if ($data{$language}{details} =~ /msgcmp: found (\d+) fatal errors?/) {
				$data{$language}{errors} = $1;
			} else {
				$data{$language}{errors} = 0;
			}
		} else {
			# TODO: report that could not open file
		}
	}
	return \%data;
}


sub save_text_report {
	my ($text_report_file) = @_;
	open my $fh, '>', $text_report_file or die;
	
	print $fh "Generated by $0 on " . localtime() . "\n\n";
	
	
	foreach my $project (sort keys %reports) {
		print $fh "--------------------\n";
		print $fh "Project $project\n\n";
		print $fh generate_text_report($reports{$project});
	}
	print "file $text_report_file generated.\n";
}

sub generate_text_report {
	my ($data) = @_;

	my $report    .= "Language  Errors\n";
	foreach my $language (sort keys %$data) {
		$report .= sprintf("%-10s %s\n", substr($language, 0, -3), $data->{$language}{errors});
	}
	
	if ($details) {
		foreach my $language (sort keys %$data) {
			$report .= "\n------------------\n";
			$report .= $language . "\n\n";
			if ($data->{$language}{errors}) {
				$report .= "Fatal errors: $data->{$language}{errors}\n\n";
			}
			$report .= $data->{$language}{details};
		}
		
	}
	return $report;
}



sub usage {
	my $msg = shift;
	print "$msg\n\n" if defined $msg;
	print <<"END_USAGE";
Usage: $0
        --text 
	--details
        --html --dir DIR

	--project         path to the project directory
	--all             all the projects
	--trunk  PATH     to root of all the projects
	
        --all and --project are mutually exclusive
END_USAGE

	exit 1;
}

